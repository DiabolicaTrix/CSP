buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = mod_version
group= "fr.scarex"
archivesBaseName = "CSP"

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "eclipse"
    
    replace "@VERSION@", project.version
    replace "@DEBUG@", true
    replaceIn "CSP.java"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "cofhcore_version", project.cofhcore_version
    inputs.property "tf_version", project.tf_version
    inputs.property "te_version", project.te_version
    
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'dependancies.info'
        
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'cofhcore_version':project.cofhcore_version, 'tf_version':project.tf_version, 'te_version':project.te_version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'dependancies.info'
    }
}


// Dependencies

repositories {
    mavenCentral()
    maven {
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name 'ProfMobius Repo'
        url "http://mobiusstrip.eu/maven/"
    }
    maven {
        name 'Player'
        url "http://maven.ic2.player.to/"
    }
    maven {
        name 'RX14'
        url "http://mvn.rx14.co.uk/repo/"
    }
    maven {
        name = "Tterrag"
        url = "http://maven.tterrag.com/"
    }
    ivy {
        name "CoFHCore"
        artifactPattern 'http://addons-origin.cursecdn.com/files/2246/919/[module]-[revision].[ext]'
    }
    ivy {
        name "ThermalFoundation"
        artifactPattern 'http://addons-origin.cursecdn.com/files/2246/921/[module]-[revision].[ext]'
    }
    ivy {
        name "ThermalExpansion"
        artifactPattern 'http://addons-origin.cursecdn.com/files/2246/924/[module]-[revision].[ext]'
    }
    ivy {
        name "ThermalDynamics"
        artifactPattern 'http://addons-origin.cursecdn.com/files/2246/949/[module]-[revision].[ext]'
    }
    ivy {
        name "MineFactoryReloaded"
        artifactPattern 'http://addons-origin.cursecdn.com/files/2233/906/[module]-[revision].[ext]'
    }
    ivy {
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
    }
}

configurations {
    api
}

dependencies {
    compile "codechicken:CodeChickenCore:${mc_version}-${ccc_version}:dev"
    api "codechicken:CodeChickenCore:${mc_version}-${ccc_version}:src"
    compile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:dev"
    compile "codechicken:ForgeMultipart:${mc_version}-${fmp_version}:dev"
    compile "codechicken:NotEnoughItems:${mc_version}-${nei_version}:dev"
    compile "mcp.mobius.waila:Waila:${waila_version}_${mc_version}"
    compile "cofh:CoFHCore:[${mc_version}]${cofhcore_version}"
    compile "cofh:ThermalFoundation:[${mc_version}]${tf_version}"
    compile "cofh:ThermalExpansion:[${mc_version}]${te_version}"
    compile "cofh:ThermalDynamics:[${mc_version}]${td_version}"
    compile "cofh:MineFactoryReloaded:[${mc_version}]${mfr_version}"
    compile "net.industrial-craft:industrialcraft-2:${ic2_version}-experimental:dev"
    compile "buildcraft:buildcraft:${bc_version}:dev"
    compile "com.enderio:EnderIO:${mc_version}-${enderio_version}:dev"
    compile "inventorytweaks:InventoryTweaks:${invtweaks_version}"
}

sourceSets {
    main {
        java {
            srcDirs += "api"
        }
    }
}

task extractFiles(type: Copy) << {
    delete "api"
    from configurations.api.collect {
        it.isDirectory() ? it : zipTree(it)
    }
    into "api/tmp/"
}

task extractApi(type: Copy, dependsOn: extractFiles) {
    configurations.api.each {
        def name = it.name.substring(0, it.name.indexOf('-')) + "_api"
        if (project.properties.containsKey(name)) {
            from "api/tmp/" + project[name]
            into "api/" + project[name]
        }
    }
    doLast {
        delete "api/tmp"
    }
}


// Artifacts

jar {
    manifest {
        attributes "FMLCorePluginContainsFMLMod": "true"
        attributes "FMLCorePlugin": "codechicken.core.launch.DepLoader"
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourceJar
    archives jar
}

tasks.assemble.dependsOn('deobfJar', 'sourceJar')


// Config

ext.privateConfigFile = file 'private.properties'

privateConfigFile.withReader {
    def p = new Properties()
    p.load(it)
    project.ext.privateConfig = new ConfigSlurper().parse p
}


// Upload

configurations {
    ftpAntTask
}

dependencies {
    ftpAntTask("org.apache.ant:ant-commons-net:1.9.4") {
        module("commons-net:commons-net:1.4.1") {
            dependencies "oro:oro:2.0.8:jar"
        }
    }
}

void ftp(Map args, Closure antFileset = {}) {
    ant {
        taskdef(name: 'ftp',
                classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
                classpath: configurations.ftpAntTask.asPath)
        Map ftpArgs = args + [
                               verbose : 'yes',
                               server  : privateConfig.ftp_adress,
                               userid  : privateConfig.ftp_user,
                               password: privateConfig.ftp_pw
        ]
        delegate.ftp(ftpArgs) {
            antFileset.delegate = delegate
            antFileset()
        }
    }
}

task upload() << {
    ftp(action: 'mkdir', remotedir: "repo/fr/scarex/${archivesBaseName}/${version}")
    ftp(action: 'send', remotedir: "repo/fr/scarex/${archivesBaseName}/${version}") {
        fileset(dir: "$buildDir/libs") {
            include(name: "${archivesBaseName}-${version}*.jar")
        }
    }
}

task mavenUpload(dependsOn: 'publish') << {
    ftp(action: 'mkdir', remotedir: "repo")
    ftp(action: 'send', remotedir: "repo") {
        fileset(dir: "$buildDir/repo")
    }
}

apply plugin: 'maven-publish'

publishing {
    publications {
        universal(MavenPublication) {
            artifact jar
        }
        dev(MavenPublication) {
            artifact deobfJar
        }
        sources(MavenPublication) {
            artifact sourceJar
        }
    }
    
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}